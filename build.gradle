plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	exclusiveContent {
		forRepository {
			maven {
				name = "Modrinth"
				url = "https://api.modrinth.com/maven"
			}
		}
		filter {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://repo.hypixel.net/repository/Hypixel/"
	}
	mavenLocal()
}

configurations {
	shaded {
		canBeResolved = true
		canBeConsumed = false
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	include(modImplementation("org.joml:joml:1.10.7"))

	modImplementation "net.hypixel:mod-api:${project.hypixel_mod_api}"
	modImplementation "maven.modrinth:hypixel-mod-api:${project.hypixel_fabric_mod_api}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	configurations.shaded.dependencies.each {
		include it
		implementation it
	}
}

tasks.register('copyShadedDependencies', Copy) {
	from configurations.shaded
	into "$buildDir/libs/shaded"
}

// Make the build task depend on copying shaded dependencies
build.dependsOn copyShadedDependencies

// Include shaded dependencies in the JAR
jar {
	from {
		configurations.shaded.collect {
			it.isDirectory() ? it : zipTree(it).matching {
				exclude 'META-INF/**'
			}
		}
	}

	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}